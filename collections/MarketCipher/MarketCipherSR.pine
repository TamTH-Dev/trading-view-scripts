// Â© tamth_
// @version=5
indicator("Market Cipher SR", overlay = true)

int pivotPrd = input.int(1, "H/L Pivot Days Back", 1)
var resolution = input.timeframe("15", "Resolution")
int volMaLen = input.int(6, "VolumeMA")
bool vwapShowed = input.bool(true, "VWAP")

requestSecurity(sym, res, src) => request.security(sym, res, src, barmerge.gaps_off, barmerge.lookahead_on)

getFractal(dir, src) =>
    fractal = 0.0
    fractal := dir ? src[3] : fractal[1]
    
pivotHigh = requestSecurity(syminfo.tickerid, "D", ta.highest(high, pivotPrd))
pivotLow  = requestSecurity(syminfo.tickerid, "D", ta.lowest(low, pivotPrd))
float centerOfRange = math.avg(pivotHigh, pivotLow)
float volMa = ta.sma(volume, volMaLen)
up = high[3] > high[4] and high[4] > high[5] and high[2] < high[3] and high[1] < high[2] and volume[3] > volMa[3], fractalUp = getFractal(up, high)
dn = low[3]  < low[4]  and low[4]  < low[5]  and low[2]  > low[3]  and low[1]  > low[2]  and volume[3] > volMa[3], fractalDn = getFractal(dn, low)
resistance = requestSecurity(syminfo.tickerid, resolution, fractalUp)
support = requestSecurity(syminfo.tickerid, resolution, fractalDn)
float vwap = ta.vwap


plot(pivotHigh, "Pivot High", color.new(#4DA6FF, 20))
plot(pivotLow, "Pivot Low", color.new(#4DA6FF, 20))
plot(centerOfRange, "Center of Range", color.new(#4DA6FF, 20), 2, plot.style_circles)
plot(resistance, "Cipher Resistance", #EB42F4, 2, plot.style_circles, offset = -1)
plot(support, "Cipher Support", #FFFB03, 2, plot.style_circles, offset = -1)
plot(vwapShowed ? vwap : na, "VWAP", #FFFFFF, 2)