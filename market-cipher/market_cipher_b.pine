// © tamth_
// @version=5
indicator("Market Cipher B", overlay = false)


// Colors
color white_color = color.white
color blue_dark_color = #082EBF
color blue_light_color = #3DCBFF
color yellow_color = #F2E40A
color green_color = #16F9BA
color red_color = #F23A64
color pink_color = #DB3AF2
color aqua_color = #21BAF3
color purple_color = #673AB7


// Inputs
string threshold_grp = "Threshold"
bool threshold_showed = input.bool(true, title = "Show", group = threshold_grp)
int overbought_threshold = input.int(60, title = "Overbought", group = threshold_grp)
int oversold_threshold = input.int(-60, title = "Oversold", group = threshold_grp)
string wt_grp = "Waves Trend"
bool wt_showed = input.bool(true, title = "Show", group = wt_grp)
int wt_chl_prd = input.int(9, title = "Channel Period", group = wt_grp)
int wt_avg_prd = input.int(12, title = "Average Period", group = wt_grp)
string mfo_grp = "MFO"
bool mfo_showed = input.bool(true, title = "Show", group = mfo_grp)
int mfo_prd = input.int(60, title = "Period", group = mfo_grp)
int mfo_mult = input.int(150, title = "Area Multiplier", group = mfo_grp)
float mfo_pos_y = input.float(2.5, title = "Position Y", group = mfo_grp)
string rsi_grp = "RSI"
bool rsi_showed = input.bool(true, title = "Show", group = rsi_grp)
int rsi_prd = input.int(40, title = "Period", group = rsi_grp)
string stoch_rsi_grp = "Stochastic RSI"
bool stoch_rsi_showed = input.bool(true, title = "Show", group = stoch_rsi_grp)
int stoch_rsi_prd = input.int(81, title = "Period", group = stoch_rsi_grp)
bool vwap_showed = input.bool(true, title = "Show VWAP")


// Methods
get_wt_and_vwap() =>
    series float src = hlc3
    float esa = ta.ema(src, wt_chl_prd)
    float d = ta.ema(math.abs(src - esa), wt_chl_prd)
    float ci = (src - esa) / (0.015 * d)
    float leading_wt = ta.ema(ci, wt_avg_prd)
    float lagging_wt = ta.sma(leading_wt, 3)
    float vwap = leading_wt - lagging_wt
    [leading_wt, lagging_wt, vwap]

get_mfo() => request.security(syminfo.tickerid, timeframe.period, ta.sma(((close - open) / (high - low)) * mfo_mult, mfo_prd) - mfo_pos_y)
        
get_indicators() =>
    int offset = 0
    [leading_wt, lagging_wt, vwap] = get_wt_and_vwap()
    float mfo = get_mfo()
    float rsi = ta.sma(ta.stoch(close, high, low, rsi_prd), 2)
    float stoch_rsi = ta.sma(ta.stoch(close, high, low, stoch_rsi_prd), 2)
    [leading_wt[offset], lagging_wt[offset], vwap[offset], mfo[offset], rsi[offset], stoch_rsi[offset]]

[leading_wt, lagging_wt, vwap, mfo, rsi, stoch_rsi] = request.security(syminfo.tickerid, timeframe.period, get_indicators())


// Plots
var space_0_line = plot(-100, title = "Space", color = color.new(white_color, 100), editable = false, display = display.pane)
var space_1_line = plot(-120, title = "Space", color = color.new(white_color, 100), editable = false, display = display.pane)
fill(space_0_line, space_1_line, title = "Space", color = color.new(white_color, 100), editable = false)

plot(threshold_showed ? overbought_threshold : na, title = "Overbought Threshold", color = color.new(white_color, 80), display = display.pane)
plot(threshold_showed ? oversold_threshold : na, title = "Oversold Threshold", color = color.new(white_color, 80), display = display.pane)

var mfo_bar_top_line = plot(mfo_showed ? -95 : na, title = "MFO Bar", color = color.new(white_color, 100), editable = false, display = display.pane)
var mfo_bar_bottom_line = plot(mfo_showed ? -100 : na, title = "MFO Bar", color = color.new(white_color, 100), editable = false, display = display.pane)
fill(mfo_bar_top_line, mfo_bar_bottom_line, title = "MFO Bar", color = mfo > 0 ? color.new(green_color, 25) : color.new(red_color, 25))

plot(wt_showed ? leading_wt : na, title = "Leading Wave Trend", color = blue_light_color, linewidth = 1, style = plot.style_area)
plot(wt_showed ? lagging_wt : na, title = "Lagging Wave Trend", color = color.new(blue_dark_color, 20), linewidth = 1, style = plot.style_area)

plot(vwap_showed ? vwap : na, title = "VWAP", color = color.new(yellow_color, 55), linewidth = 1, style = plot.style_area)

plot(mfo_showed ? mfo : na, title = "MFO", color = mfo > 0 ? color.new(green_color, 60) : color.new(red_color, 60), linewidth = 2, style = plot.style_area)

plot(0, title = "Zero Line", color = white_color, display = display.pane)

plot(rsi_showed ? rsi : na, title = "RSI", color = pink_color)

plot(stoch_rsi_showed ? stoch_rsi : na, title = "Stochastic RSI", color = rsi > stoch_rsi ? aqua_color : purple_color, linewidth = 2)

plot(ta.crossover(leading_wt, lagging_wt) ? lagging_wt : na, title = "Buy Signal", color = color.new(green_color, 10), linewidth = 3, style = plot.style_circles, display = display.pane)
plot(ta.crossunder(leading_wt, lagging_wt) ? lagging_wt : na, title = "Sell Signal", color = color.new(red_color, 10), linewidth = 3, style = plot.style_circles, display = display.pane)

plotchar(ta.crossover(leading_wt, lagging_wt) and leading_wt < oversold_threshold, title = "Strong Buy Signal", char = "⛳", color = green_color, location = location.bottom, size = size.tiny, display = display.pane)
plotchar(ta.crossover(lagging_wt, leading_wt) and leading_wt > overbought_threshold, title = "Strong Sell Signal", char = "⛳", color = red_color, location = location.bottom, size = size.tiny, display = display.pane)